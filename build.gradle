apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'maven-publish'


version = '0.1.26'
description = 'LBU-BB-Tasks'
group = 'uk.ac.leedsbeckett'

def bbhandle = 'bbtasks'
def bbname = 'LBU-BB-Tasks'
def bbdescription = 'LBU BB Tasks.'
def vendorid = 'LBU'
def vendorname = 'Leeds Beckett University'
def vendordescription = 'Leeds Beckett University'
def vendorurl = 'http://www.leedsbeckett.ac.uk/'


// Ensure compatible with Java 11 even if using newer compiler.
java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}


clean {
    delete "${buildDir}/webappconfig"
}

war.doFirst {
  copy {
    from "src/main/webappconfig"
    into "${buildDir}/webappconfig"
    expand([
     bbhandle: bbhandle,
     bbname: bbname,
     version: version,
     vendorid: vendorid,
     vendorname: vendorname,
     vendordescription: vendordescription,
     vendorurl: vendorurl,
     description: bbdescription
    ])
  }
}
war {
  webInf { from "${buildDir}/webappconfig" }
}

repositories {
    jcenter()
    maven {
      url "https://maven.blackboard.com/content/repositories/releases/"
    }    
    maven {
      url "https://maven.pkg.github.com/leeds-beckett-digital-learning/lbu-bb-b2-utils"
          credentials {
              username = gitHubUserName
              password = gitHubPrivateToken // the variable resides in ~/.gradle/gradle.properties
          }
    }
}

dependencies {
    compileOnly "javax.servlet:javax.servlet-api:3.1.0"
    compileOnly "javax.websocket:javax.websocket-api:1.1"  
    compileOnly "blackboard.platform:bb-platform:3900.19.0"
    compileOnly "blackboard.platform:xsscore:3900.19.0"
    implementation "log4j:log4j:1.2.17"
    compileOnly( "javax.jms:jms:1.1" )
    compileOnly "com.sun.mail:javax.mail:1.6.2"
    compileOnly "com.amazonaws:aws-java-sdk-core:1.11.483"
    compileOnly "org.apache.httpcomponents:httpclient:4.5.12"
    compileOnly fileTree(dir: 'locallibs', include: '*.jar')
    
    implementation 'uk.ac.leedsbeckett:bbb2utils:0.2.11'
    
    implementation 'org.apache.commons:commons-text:1.9'
    implementation 'org.apache.commons:commons-compress:1.20'    
    implementation 'org.apache.commons:commons-csv:1.9.0'    
    implementation 'commons-fileupload:commons-fileupload:1.4'

    implementation 'commons-io:commons-io:2.8.0'
    implementation 'com.drewnoakes:metadata-extractor:2.15.0'
    testImplementation 'junit:junit:4.12'
}

publishing {
    publications {
        library(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url "https://maven.pkg.github.com/leeds-beckett-digital-learning/lbu-bb-tasks"
            credentials {
                username = gitHubUserName
                password = gitHubPrivateToken // the variable resides in ~/.gradle/gradle.properties
            }
        }
    }
}
